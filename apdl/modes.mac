! *****************************************************************************
! Write natural frequencies and displacements of the eigenmodes to the file 
! 'modes.dat' of selected nodes. These nodes are the closest(!) ones to the 
! coordinates listed in the file 'coordsMP.dat'. The coordinates are with 
! respect to the coordinate system 1000. Additionally, the file 'errorMP.dat'
! contains, beside the node numbers, also the distance between the selected 
! node and the defined coordinates. 
!
! (c)by FJ
! *****************************************************************************

! set last result
SET, last

! GET NODE NUMBERS OF MEASUREMENT POINTS
! number of lines in file coordinates.dat
/INQUIRE, numMP, lines, coordsMP, dat

! read coordinates of measurement points
*DIM, nodeCoord, array, numMP, 3

! read nodes, read row-wise jik:
*VREAD, nodeCoord(1,1), coordsMP, dat, , jik, 3, numMP
(3F20.4)

! select coordinate frame:
CSYS, 1000 
RSYS, 1000

! get node numbers
*DIM, nodeNum, array, numMP, 2
*DO, i, 1, numMP
    ! node numbers of measurement points
    nodeNum(i,1) = node(nodeCoord(i,1),nodeCoord(i,2),nodeCoord(i,3))
    
    ! distance between original and actual node for measurement point
    x = nx(nodeNum(i)) 
    y = ny(nodeNum(i))
    z = nz(nodeNum(i))
    nodeNum(i,2) = sqrt((nodeCoord(i,1)-x)**2+(nodeCoord(i,2)-y)**2+(nodeCoord(i,3)-z)**2)
*ENDDO

! write to file
*CFOPEN, errorMP, dat
*VWRITE, nodeNum(1,1), nodeNum(1,2)
(F15.0, F10.4) 
*CFCLOS


! GET EIGENMODES
! open file to write
*CFOPEN, modes, dat
*VWRITE, -100
(F10.0)

! number of computed modes
*GET, numModes, active, 0, set, sbst

*DIM, modeShapes, array, numMP, 3

! loop over all modes
*DO, i, 1, numModes
    
    ! select next eigenmode
    SET, 1, i

    ! get natural frequency
    *GET, natFreq, active, 0, set, freq 
    *VWRITE, natFreq
    (F10.2)
    
    ! get displacements at nodes
    *DO, j, 1, numMP
        modeShapes(j,1) = ux(nodeNum(j,1))
        modeShapes(j,2) = uy(nodeNum(j,1))
        modeShapes(j,3) = uz(nodeNum(j,1))
    *ENDDO
    
    ! write to file
    *VWRITE, modeShapes(1,1), modeShapes(1,2), modeShapes(1,3)
    (3F20.4)
    
    ! delimiter
    *VWRITE, -100
    (F10.0)

*ENDDO

! close file 
*CFCLO

! copy file to user_files
*DIM, fname, string, 128
fname(1) = strcat(_WB_USERFILES_DIR(1),'modes')
/copy, modes, dat, , fname(1), dat
fname(1) = strcat(_WB_USERFILES_DIR(1),'errorMP')
/copy, errorMP, dat, , fname(1), dat